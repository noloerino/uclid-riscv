
module main {
    type * = common.*;
    define * = common.*;
    type * = instructions.*; // needed to fix enum import bug
    const * = registers.*;

    var reset : boolean;
    var imem : mem_t;
    var step : integer;

    instance impl : Datapath (reset : (reset), imem : (imem));
    instance spec : cpu (imem : (imem));

    type pipe_state_t = enum { UNSTARTED, WAITING, EXEC, COMMITTED, DONE };

    var i_state : pipe_state_t;
    var s_state : pipe_state_t;
    var shadow_start_pc : pc_t;
    var shadow_impl_rf : regfile_t;
    var shadow_spec_pc : pc_t;
    var shadow_spec_rf : regfile_t;

    const IMEM_PC_START : bv30 = 0x20000000bv30;
    const PC_START : bv32 = 0x80000000bv32;

    const GOAL_PC : bv32 = 0x80000004bv32;

    init {
        reset = true;
        assume (imem[IMEM_PC_START + 0bv30] == instructions.NOP);
        assume (imem[IMEM_PC_START + 1bv30] == 0x00036097bv32); // auipc ra, 54
        assume (imem[IMEM_PC_START + 2bv30] == instructions.NOP);
        assume (imem[IMEM_PC_START + 3bv30] == instructions.NOP);
        assume (imem[IMEM_PC_START + 4bv30] == instructions.NOP);
        assume (imem[IMEM_PC_START + 5bv30] == instructions.NOP);
        // assume (forall (a : mem_word_addr_t) :: ((a < IMEM_PC_START || a > IMEM_PC_START + 1bv30) ==> imem[a] == instructions.NOP)); // all else nops
        // assume (forall (a : mem_word_addr_t) :: ((a > IMEM_PC_START + 1bv30) ==> imem[a] == instructions.NOP)); // all else nops
        step = 0;
        i_state = UNSTARTED;
        s_state = UNSTARTED;
    }

    next {
        if (reset) {
            reset' = false;
        }
        case
            (i_state == UNSTARTED) : {
                i_state' = WAITING;
            }
            (i_state == WAITING) : {
                if (!(impl.started || impl.fe_ctrl.inst_kill || impl.ew_br_taken) && impl.fe_pc == GOAL_PC) {
                    shadow_start_pc' = impl.fe_pc;
                    i_state' = EXEC;
                }
            }
            (i_state == EXEC) : {
                if (impl.ew_pc == shadow_start_pc) {
                    i_state' = COMMITTED;
                }
            }
            (i_state == COMMITTED) : {
                shadow_impl_rf' = impl.regs;
                i_state' = DONE;
            }
        esac
        case
            (s_state == UNSTARTED || s_state == WAITING) : {
                if (spec.pc == GOAL_PC) {
                    shadow_spec_pc' = spec.pc;
                    s_state' = EXEC;
                }
            }
            (s_state == EXEC) : {
                shadow_spec_rf' = spec.regfile;
                s_state' = DONE;
            }
        esac
        if (i_state == DONE && s_state == DONE) {
            // assert (false);
                // 0x36000 = 54 << 12
                // assert (spec.pc == 0bv32);
                // assert (cpu_0.rf_read[ra] == PC_START + 4bv32 + 0x36000bv32);
        }
        if (i_state != DONE) {
            next(impl);
        }
        if (s_state != DONE) {
            next(spec);
        }
        step' = step + 1;
    }

    invariant reaches_done : step == 5 ==> (i_state == DONE && s_state == DONE);
    invariant when_done : (i_state == DONE && s_state == DONE) ==>
        (shadow_start_pc == shadow_spec_pc && shadow_impl_rf == shadow_spec_rf);
    invariant pc_aligned : i_state != DONE ==> impl.fe_pc[1:0] == 0bv2;

    control {
        vobj = bmc_noLTL(5); // FIX: why does 'bmc' here not work?
        check;
        print_results;
        vobj.print_cex(
            reset,
            step,
            imem,
            impl.fe_pc,
            impl.fe_inst,
            impl.fe_ctrl,
            impl.ew_pc,
            impl.ew_inst,
            impl.ew_ctrl,
            impl.ew_imm,
            impl.ew_br_taken,
            impl.ew_rd,
            impl.ew_rs2_val,
            impl.ew_alu_out,
            impl.regs,
            impl.started,
            i_state,
            s_state,
            shadow_start_pc,
            shadow_impl_rf,
            shadow_spec_pc,
            shadow_spec_rf
        );
    }
}

