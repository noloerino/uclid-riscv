// Performs refinement checks for all instructions, synthesizing them according to the relevant
// instructions fields.

module main {
    type * = common.*;
    define * = common.*;
    define * = encdec.*;
    type * = instructions.*; // needed to fix enum import bug
    const * = registers.*;
    const * = encdec.*;

    var imem : mem_t;
    var step : integer;
    var flush : boolean;

    instance impl : Datapath (imem : (imem), flush : (flush));
    instance spec : cpu (imem : (imem));

    instance ec : encdec ();

    type pipe_state_t = enum { UNSTARTED, WAITING, FLUSH, COMMIT, DONE };

    var i_state : pipe_state_t;
    var s_state : pipe_state_t;
    var shadow_impl_pc : pc_t;
    var shadow_impl_rf : regfile_t;
    var shadow_spec_pc0 : pc_t;
    var shadow_spec_pc1 : pc_t;
    var shadow_spec_rf0 : regfile_t;
    var shadow_spec_rf1 : regfile_t;

    var test_inst_0 : inst_t;
    var test_inst_1 : inst_t;
    var test_inst_2 : inst_t;
    var test_inst_3 : inst_t;
    var test_inst_4 : inst_t;
    var test_inst_5 : inst_t;
    var test_inst_6 : inst_t;
 
    const IMEM_PC_START : bv30 = 0x20000000bv30;
    const PC_START : bv32 = 0x80000000bv32;

    const GOAL_PC : bv32 = 0x80000010bv32;

    procedure random_inst() returns (new_inst : inst_t) modifies ec; {
        // randomly generate an instruction
        var rtype_i : rtype_t;
        var itype_arith_i : itype_arith_t;
        var itype_shift_i : itype_shift_t;
        var itype_mem_i : itype_mem_t;
        var stype_i : stype_t;
        var btype_i : btype_t;
        var utype_i : utype_t;
        var jal_i : jal_t;
        var jalr_i : jalr_t;
        // 9 possible instruction types to choose
        var i_choice : bv4;
        // TODO avoid generating improper alignment memory ops for now
        assume (itype_mem_i.inst == LB || itype_mem_i.inst == LBU);
        assume (stype_i.inst == SB);
        // avoid generating misaligned jump targets
        // also avoid 0 jumps, which mess up the PC checks
        assume (btype_i.imm[1:0] == 0bv2 && btype_i.imm != 0bv13);
        assume (jal_i.imm[1:0] == 0bv2 && jal_i.imm != 0bv21);
        assume (jalr_i.imm[1:0] == 0bv2 && jalr_i.imm != 0bv12);
        // instruction encoding
        case
            (i_choice == 0bv4) : { call (new_inst) = ec.encode_rtype(rtype_i); }
            (i_choice == 1bv4) : { call (new_inst) = ec.encode_itype_arith(itype_arith_i); }
            (i_choice == 2bv4) : { call (new_inst) = ec.encode_itype_shift(itype_shift_i); }
            (i_choice == 3bv4) : { call (new_inst) = ec.encode_itype_mem(itype_mem_i); }
            (i_choice == 4bv4) : { call (new_inst) = ec.encode_stype(stype_i); }
            (i_choice == 5bv4) : { call (new_inst) = ec.encode_btype(btype_i); }
            (i_choice == 6bv4) : { call (new_inst) = ec.encode_utype(utype_i); }
            (i_choice == 7bv4) : { call (new_inst) = ec.encode_jal(jal_i); }
            (i_choice == 8bv4) : { call (new_inst) = ec.encode_jalr(jalr_i); }
            default            : { new_inst = instructions.NOP; }
        esac
    }

    init {
        flush = false;
        call (test_inst_0) = random_inst();
        call (test_inst_1) = random_inst();
        call (test_inst_2) = random_inst();
        call (test_inst_3) = random_inst();
        call (test_inst_4) = random_inst();
        call (test_inst_5) = random_inst();
        call (test_inst_6) = random_inst();

        assume (imem[IMEM_PC_START + 0bv30] == test_inst_0);
        assume (imem[IMEM_PC_START + 1bv30] == test_inst_1);
        assume (imem[IMEM_PC_START + 2bv30] == test_inst_2);
        assume (imem[IMEM_PC_START + 3bv30] == test_inst_3);
        assume (imem[IMEM_PC_START + 4bv30] == test_inst_4);
        assume (imem[IMEM_PC_START + 5bv30] == test_inst_5);
        assume (imem[IMEM_PC_START + 6bv30] == test_inst_6);

        // assume (!(exists (a : mem_word_addr_t) :: ((a < IMEM_PC_START || a > IMEM_PC_START + 1bv30) && imem[a] != instructions.NOP))); // all else nops

        // TODO parameterize DMEM and regfile as arguments for model
        assume (impl.dmem == spec.dmem);
        step = 0;
        i_state = UNSTARTED;
        s_state = UNSTARTED;
    }

    next {
        assume (i_state == UNSTARTED || (impl.fe_pc <_u PC_START + 28bv32 && impl.fe_pc >=_u PC_START));
        assume (spec.pc <_u PC_START + 28bv32 && spec.pc >=_u PC_START);
        assume (impl.fe_pc[1:0] == 0bv2);
        // assume (impl.ew_pc <_u PC_START + 28bv32 && impl.ew_pc >=_u PC_START - 4bv32);
        case
            (i_state == UNSTARTED) : {
                i_state' = WAITING;
            }
            (i_state == WAITING) : {
                if (impl.fe_pc == GOAL_PC && !impl.fe_ctrl.killed) {
                    flush' = true;
                    i_state' = FLUSH;
                }
            }
            (i_state == FLUSH) : {
                flush' = false;
                i_state' = COMMIT;
            }
            (i_state == COMMIT) : {
                shadow_impl_pc' = if (impl.ew_br_taken || impl.ew_ctrl.pc_sel == ControlLogic.PC_SEL_ALU) then impl.npc else impl.fe_pc;
                shadow_impl_rf' = impl.regs;
                i_state' = DONE;
            }
        esac
        case
            (s_state == UNSTARTED) : {
                if (spec.pc == GOAL_PC) {
                    shadow_spec_pc0' = spec.pc;
                    shadow_spec_rf0' = spec.regfile;
                    s_state' = WAITING;
                }
            }
            (s_state == WAITING) : {
                shadow_spec_pc1' = spec.pc;
                shadow_spec_rf1' = spec.regfile;
                s_state' = DONE;
            }
        esac
        if (i_state != DONE) {
            next(impl);
        }
        if (s_state != DONE) {
            next(spec);
        }
        step' = step + 1;
    }

    define spec_ok() : boolean = spec.exception.cause == X_NONE;

    // Because we have 6 free instructions, we actually can synthesize loops
    // The only problem is that unless GOAL_PC is the address of the first instruction,
    // we cannot guarantee that the program reaches GOAL_PC, e.g. if one of the instructions
    // is beq x0, x0, 0 and loops forever
    invariant reaches_done : (step == 5) ==> (i_state == DONE && s_state == DONE);
    invariant when_done : (spec_ok() && i_state == DONE && s_state == DONE) ==>
        ((shadow_impl_pc == shadow_spec_pc0 && shadow_impl_rf == shadow_spec_rf0) ||
         (shadow_impl_pc == shadow_spec_pc1 && shadow_impl_rf == shadow_spec_rf1))
        ;
    invariant pc_aligned : (spec_ok() && i_state != DONE) ==> impl.fe_pc[1:0] == 0bv2;

    control {
        vobj = bmc_noLTL(5); // FIX: why does 'bmc' here not work?
        check;
        print_results;
        vobj.print_cex(
            test_inst_0,
            test_inst_1,
            test_inst_2,
            test_inst_3,
            test_inst_4,
            test_inst_5,
            test_inst_6,
            flush,
            step,
            imem,
            impl.npc,
            impl.fe_pc,
            impl.fe_inst,
            impl.fe_ctrl,
            impl.ew_pc,
            impl.ew_inst,
            impl.ew_ctrl,
            impl.ew_imm,
            impl.ew_br_taken,
            impl.ew_rd,
            impl.ew_rs2_val,
            impl.ew_alu_out,
            impl.regs,
            impl.dmem,
            impl.illegal,
            i_state,
            s_state,
            spec.pc,
            spec.exception,
            spec.dmem,
            shadow_impl_pc,
            shadow_impl_rf,
            shadow_spec_pc0,
            shadow_spec_rf0,
            shadow_spec_pc1,
            shadow_spec_rf1
        );
    }
}

