
module main {
    type * = common.*;
    define * = common.*;
    type * = instructions.*; // needed to fix enum import bug
    const * = registers.*;
    const * = cpu.*;

    var imem : mem_t;
    var step : integer;

    instance cpu_0 : cpu (imem : (imem));

    init {
        assume (imem[IMEM_PC_START + 0bv30] == 0x00000013bv32); // nop
        assume (imem[IMEM_PC_START + 1bv30] == 0x06400413bv32); // addi s0, x0, 100
        assume (imem[IMEM_PC_START + 2bv30] == 0x00000513bv32); // addi a0, x0, 0
        assume (imem[IMEM_PC_START + 3bv30] == 0x00000593bv32); // addi a1, x0, 0
        assume (imem[IMEM_PC_START + 4bv30] == 0x06300493bv32); // addi s1, x0, 100
        assume (imem[IMEM_PC_START + 5bv30] == 0x00941463bv32); // bne  s0, s1, good
        assume (imem[IMEM_PC_START + 6bv30] == 0xC1800513bv32); // addi a0, x0, -1000
        assume (imem[IMEM_PC_START + 7bv30] == 0x3E800593bv32); // good: addi a1, x0, 1000
        assume (forall (a : mem_word_addr_t) :: (a < IMEM_PC_START || a > IMEM_PC_START + 7bv30) ==> imem[a] == instructions.NOP);
        step = 0;
    }

    next {
        next (cpu_0);
        case
            (step == 7) : { // nop, addi x4, b, addi
                assert (cpu_0.regfile[a1] == 1000bv32);
            }
        esac
        step' = step + 1;
    }

    invariant no_exception : cpu_0.exception.cause == X_NONE;
    invariant bad_missed : step > 2 ==> cpu_0.regfile[a0] == 0bv32;

    control {
        vobj = bmc_noLTL(8); // FIX: why does 'bmc' here not work?
        check;
        print_results;
        vobj.print_cex(step, cpu_0.exception, cpu_0.pc, imem, cpu_0.regfile);
    }
}

