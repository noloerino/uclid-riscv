
module main {
    type * = common.*;
    define * = common.*;
    type * = instructions.*; // needed to fix enum import bug
    const * = registers.*;
    const * = cpu.*;

    var imem : mem_t;
    var step : integer;

    instance cpu_0 : cpu (imem : (imem));

    init {
        assume (imem[IMEM_PC_START + 0bv30] == 0x00000013bv32); // nop
        assume (imem[IMEM_PC_START + 1bv30] == 0x06400593bv32); // addi a1, x0, 100
        assume (imem[IMEM_PC_START + 2bv30] == 0x0C800613bv32); // addi a2, x0, 200
        assume (imem[IMEM_PC_START + 3bv30] == 0x06466593bv32); // ori  a1, a2, 100
        step = 0;
    }

    next {
        next (cpu_0);
        case
            (step == 1) : {
                assert (cpu_0.regfile[a1] == 100bv32);
            }
            (step == 2) : {
                assert (cpu_0.regfile[a2] == 200bv32);
            }
            (step == 3) : {
                assert (cpu_0.regfile[a1] == 236bv32);
            }
        esac
        step' = step + 1;
    }

    invariant no_exception : cpu_0.exception.cause == X_NONE;

    control {
        vobj = bmc_noLTL(4); // FIX: why does 'bmc' here not work?
        check;
        print_results;
        vobj.print_cex(step, cpu_0.exception, cpu_0.pc, imem, cpu_0.regfile);
    }
}

